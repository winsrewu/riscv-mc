import ./num_to_binary_array.js
import ./argument_parser.js
import ./bitops_t.mcbt

# the args should be the storage path of the array, connecting the namespace and path with "@", for example:
# yournamespace:temp a => "yournamespace:temp@a"

# args:
# addr: [...] (32 bit length array)

# result:
# data: [0b,1b,1b,0b,1b,1b,1b,1b] (8 bit length array)
template get {
    with addr:word data:word {
        template to_str 32 <% addr %> riscvmc_memory:temp@str
        data modify storage <% data.replace("@", " ") %> set value <% from_num(0, 8) %>

        block { with storage riscvmc_memory:temp
            $data modify storage <% data.replace("@", " ") %> set from storage riscvmc:memory m.$(str)
        }
    }
}


# args:
# addr: [...] (32 bit length array)
# data: [0b,1b,1b,0b,1b,1b,1b,1b] (8 bit length array)

# result:
# -
template put {
    with addr:word data:word {
        template to_str 32 <% addr %> riscvmc_memory:temp@str

        block { with storage riscvmc_memory:temp
            $data modify storage riscvmc:memory m.$(str) set from storage <% data.replace("@", " ") %>
        }
    }
}


# args:
# id: [...] (5 bit length array)

# result:
# data: [...] (32 bit length array)
template get_reg {
    with id:word data:word {
        template to_str 5 <% id %> riscvmc_memory:temp@str
        data modify storage <% data.replace("@", " ") %> set value <% from_num(0, 32) %>

        block { with storage riscvmc_memory:temp
            $data modify storage <% data.replace("@", " ") %> set from storage riscvmc:memory reg.$(str)
        }
    }
}


# args:
# id: [...] (5 bit length array)
# data: [...] (32 bit length array)

# result:
# -
template put_reg {
    with id:word data:word {
        template to_str 5 <% id %> riscvmc_memory:temp@str

        execute unless data storage riscvmc_memory:temp {str: "00000"} run { with storage riscvmc_memory:temp
            $data modify storage riscvmc:memory reg.$(str) set from storage <% data.replace("@", " ") %>
        }
    }
}